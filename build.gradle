//
// Generic
//
subprojects {
    group = "org.apache.cloudstack"
    version = "3.0.3"

    repositories {
        mavenCentral()
    }
}

//
// Java subprojects
//
configure(subprojects.findAll {(new File(it.projectDir, '/src/')).isDirectory()}) {

    println "Adding " + it.name + " as java project"

    apply plugin: 'java'

    archivesBaseName = 'cloudstack-' + it.name
    
    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDir 'resources'
            }

        }
        test {
            java {
                srcDir 'test'
            }
        }
    }

    jar {
        destinationDir = new File(rootDir, '/target/jar')
    }

   // Global for now, but should move to individual projects
   dependencies {
       compile "org.apache.axis:axis:1.4"
       compile "backport-util-concurrent:backport-util-concurrent:3.0"
       compile "org.bouncycastle:bcprov-jdk16:1.45"
       compile "cglib:cglib:2.2.2"
       compile "commons-codec:commons-codec:1.5"
       compile "commons-collections:commons-collections:1.3.1"
       compile "commons-configuration:commons-configuration:1.8"
       compile "commons-dbcp:commons-dbcp:1.4"
       compile "commons-discovery:commons-discovery:0.2"
       compile "commons-httpclient:commons-httpclient:3.1"
       compile "commons-lang:commons-lang:2.6"
       compile "commons-logging:commons-logging:1.1.1"
       compile "commons-pool:commons-pool:1.5.6"
       compile "net.sf.ehcache:ehcache:1.5.0"
       compile "javax.ejb:ejb-api:3.0"
       compile "com.google.code.gson:gson:1.7.1"
       compile "org.apache.httpcomponents:httpcore:4.0.1"
       compile "org.jasypt:jasypt:1.8"
       compile "com.jcraft:jsch:0.1.42"
       compile "jstl:jstl:1.2"
       compile "log4j:log4j:1.2.17"
       compile "log4j:apache-log4j-extras:1.1"
       compile "mysql:mysql-connector-java:5.1.8"
       compile "wsdl4j:wsdl4j:1.6.2"
       compile "org.apache.xmlrpc:xmlrpc-client:3.1.3"
       compile "com.thoughtworks.xstream:xstream:1.3.1"
       compile "org.mortbay.jetty:jetty-util:6.1.26"
       compile "org.mortbay.jetty:jetty-j2se6:6.1.26"
       compile "javax.mail:mail:1.4.2"

       // compile only, provider by tomcat
       compile "javax.servlet:servlet-api:2.5"
       
       // not on central repo (yet)
       // compile "org.libvirt:libvirt:0.4.8"
       compile files ('../deps/libvirt-0.4.8.jar')
       // compile "com.trilead:trilead-ssh2:build213"
       compile files ('../deps/cloud-trilead-ssh2-build213.jar')
       // compile "javax.persistence:persistence:2.0.0"
       compile files ('../deps/cloud-javax.persistence-2.0.0.jar')

       // Not available on maven repos
       compile files ('../deps/cloud-xenserver-5.6.100-1.jar')
       compile files ('../deps/cloud-jnetpcap.jar')
       compile files ('../deps/cloud-iControl.jar')
       compile files ('../deps/cloud-manageontap.jar')
       compile files ('../deps/vmware-vim.jar')
       compile files ('../deps/vmware-vim25.jar')
       compile files ('../deps/vmware-lib-jaxrpc.jar')
       compile files ('../deps/vmware-apputils.jar')

       testCompile "junit:junit:4.8.2"
    }
}

//----------------------------------
// Generic dependencies for plugins
//----------------------------------
configure(subprojects.findAll {it.path.startsWith(':plugins') && (new File(it.projectDir.absolutePath + '/src/')).isDirectory()}) {
    archivesBaseName = 'cloudstack-plugin-' + it.name
    dependencies {
        compile project(':server')
        compile project(':core')
        compile project(':utils')
        compile project(':api')
    }
       
}

project(':console-proxy') {
    dependencies {
    }
}

project(':utils') {
    dependencies {
    }
}

project(':api') {
    dependencies {
        compile project(':utils')
    }
}

project(':core') {
    dependencies {
        compile project(':utils')
        compile project(':api')
    }
}

project(':vmware-base') {
    dependencies {
        compile project(':api')
    }
}


project(':server') {
    dependencies {
        compile project(':core')
        compile project(':utils')
        compile project(':api')
        compile project(':agent')
    }
}

project(':agent') {
    dependencies {
        compile project(':core')
        compile project(':utils')
        compile project(':api')
        testCompile project(':utils')
    }
}

project(':usage') {
    dependencies {
        compile project(':core')
        compile project(':utils')
        compile project(':api')
        compile project(':server')
    }
}


//--------------------------------------------
// Special project
// should not be included unless while testing
//---------------------------------------------
project(':test') {
    dependencies {
       compile project(':core')
       compile project(':utils')
       compile project(':api')
       compile project(':server')
       compile files ('../tools/tooljars/cloud-selenium-java-client-driver.jar')
       compile "junit:junit:4.8.2"
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDir 'resources'
            }

        }
        test {
            java {
                srcDir 'src'
            }
        }
    }


    jar.enabled = false;
}

//--------------------------------------------
// Special dependencies for plugins
//---------------------------------------------
project(':plugins:hypervisors:xen') {
    dependencies {
        compile project(':plugins:network-elements:ovs')
    }
}

project(':plugins:hypervisors:vmware') {
    dependencies {
        compile project(':vmware-base')
    }
}

project('plugins:network-elements:netscaler') {
    dependencies {
       compile files ('../../../deps/cloud-netscaler.jar')
       compile files ('../../../deps/cloud-netscaler-sdx.jar')
    }
}

//--------------------------------------------
// Additional things to do during clean
//---------------------------------------------
task clean(type: Delete) {
    delete 'target' 
}
        

